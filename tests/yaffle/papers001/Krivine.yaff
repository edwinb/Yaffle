
data Ty : Type where
  Base : Ty
  Arr : (a, b : Ty) -> Ty

namespace List

  public export
  data List : Type -> Type where
    Nil : {0 a : Type} -> List a
    Cons : {0 a : Type} -> a -> List a -> List a

Context : Type
Context = List Ty

data Elem : a -> List a -> Type where
  Here : Elem x (Cons x xs)
  There : Elem x xs -> Elem x (Cons y xs)

data Term : Context -> Ty -> Type where
  Lam : Term (Cons a g) b -> Term g (Arr a b)
  App : {a : Ty} -> Term g (Arr a b) -> Term g a -> Term g b
  Var : Elem a g -> Term g a

data Closed : Ty -> Type where
data Env : Context -> Type where

data Closed : Ty -> Type where
  Closure : Term g a -> Env g -> Closed a
  ClApp : {a : Ty} -> Closed (Arr a b) -> Closed a -> Closed b

data Env : Context -> Type where
  Nil : Env Nil
  Cons : {a : Ty} -> Closed a -> Env g -> Env (Cons a g)

namespace Value

  public export
  data IsValue : Closed g -> Type where
    Lam : {0 b, env : _} -> IsValue (Closure (Lam b) env)

  public export
  data Value : Ty -> Type where
    Val : {0 a : Ty} -> (c : Closed a) -> IsValue c -> Value a

data Redex : Ty -> Type where
  RVar : {0 a, g : _} -> Elem a g -> Env g -> Redex a
  RApp : {0 b, g : _} -> {a : Ty} -> Term g (Arr a b) -> Term g a -> Env g -> Redex b
  Beta : {0 b, g : _} -> {a : Ty} -> Term (Cons a g) b -> Env g -> Closed a -> Redex b


redex : Redex a -> Closed a
redex (RVar v env) = Closure (Var v) env
redex (RApp f t env) = Closure (App f t) env
redex (Beta b env arg) = ClApp (Closure (Lam b) env) arg

lookup : Env g -> Elem a g -> Closed a
lookup (Cons v _) Here = v
lookup (Cons _ vs) (There p) = lookup vs p

contract : Redex a -> Closed a
contract (RVar v env) = lookup env v
contract (RApp f t env) = ClApp (Closure f env) (Closure t env)
contract (Beta b env arg) = Closure b (Cons arg env)

namespace EvalContext

  public export
  data EvalContext : (inner, outer : Ty) -> Type where
    Nil : {0 inner : _} -> EvalContext inner inner
    Cons : {0 a, inner, outer : _} -> Closed a -> EvalContext inner outer -> EvalContext (Arr a inner) outer


  public export
  snoc : {0 inner, dom, outer : _} -> EvalContext inner (Arr dom outer) -> Closed dom -> EvalContext inner outer
  snoc Nil t = Cons t Nil
  snoc (Cons hd ctx) t = Cons hd (snoc ctx t)

  public export
  data SnocView : EvalContext inner outer -> Type where
    Lin : SnocView Nil
    Snoc : {0 inner, dom, outer : _} ->
           (ctx : EvalContext inner (Arr dom outer)) ->
           (t : Closed dom) ->
           SnocView (snoc ctx t)

  public export
  snocView : {0 inner, outer : _} -> (ctx : EvalContext inner outer) -> SnocView ctx
  snocView Nil = Lin
  snocView (Cons hd ctx) with (snocView ctx)
    snocView (Cons hd _) | Lin = Snoc Nil hd
    snocView (Cons hd _) | Snoc ctx' t = Snoc (Cons hd ctx') t


plug : {a : Ty} -> EvalContext a b -> Closed a -> Closed b
plug Nil f = f
plug (Cons t ts) f = plug ts (ClApp f t)


namespace Builtin

  public export
  data HEqual : {0 a, b : Type} -> a -> b -> Type where
    Refl : {0 a : Type} -> {0 x : a} -> HEqual x x

  public export
  Equal :  {0 a : Type} -> a -> a -> Type
  Equal x y = HEqual x y

0 plugSnoc : (ctx : EvalContext a (Arr dom b)) ->
           (t : Closed dom) ->
           (f : Closed a) ->
           Equal (plug (snoc ctx t) f) (ClApp (plug ctx f) t)
plugSnoc Nil _ _ = Refl
plugSnoc (Cons t ctx) _ _ = plugSnoc ctx _ _



data Decomposition : Closed a -> Type where
  Val : (sc : Term (Cons a g) b) -> (env : Env g) ->
        Decomposition {a = Arr a b} (Closure (Lam sc) env)
  Red : {s : Ty} -> (r : Redex s) -> (ctx : EvalContext s t) ->
        Decomposition {a = t} (plug ctx (redex r))

load : {a : Ty} -> (ctx : EvalContext a b) -> (c : Closed a) -> Decomposition (plug ctx c)

unload : {a, b : Ty} -> (ctx : EvalContext (Arr a b) outer) ->
         (sc : Term (Cons a g) b) ->
         (env : Env g) ->
         Decomposition (plug ctx (Closure (Lam sc) env))

load ctx (ClApp f t) = load (Cons t ctx) f
load ctx (Closure (Var v) env) = Red (RVar v env) ctx
load ctx (Closure (App f t) env) = Red (RApp f t env) ctx
load ctx (Closure (Lam sc) env) = unload ctx sc env

unload Nil sc env = Val sc env
unload (Cons arg ctx) sc env = Red (Beta sc env arg) ctx

decompose : {a : Ty} -> (c : Closed a) -> Decomposition c
decompose = load Nil

decomposePlug : {a : Ty} -> (ctx : EvalContext a b) -> (c : Closed a) ->
                Equal (decompose (plug ctx c)) (load ctx c)
decomposePlug Nil c = Refl
decomposePlug (Cons t ctx) c = decomposePlug ctx (ClApp c t)

recompose : Decomposition {a} c -> Closed a
recompose (Red r ctx) = plug ctx (contract r)
recompose (Val sc env) = Closure (Lam sc) env

headReduce : {a : Ty} -> Closed a -> Closed a
headReduce c = recompose (decompose c)

loadRedex :
  (ctx : EvalContext a b) -> (r : Redex a) ->
  Equal (load ctx (redex r)) (Red r ctx)
loadRedex ctx (RVar _ _) = Refl
loadRedex ctx (RApp _ _ _) = Refl
loadRedex ctx (Beta _ _ _) = Refl

replace : {0 x, y : a} -> (0 p : _) ->
          (0 rule : Equal x y) -> (1 val : p x) -> p y
replace p Refl prf = prf

sym : Equal x y -> Equal y x
sym Refl = Refl

rewrite__impl : {0 x, y : a} -> (0 p : _) ->
                (0 rule : Equal x y) -> (1 val : p y) -> p x
rewrite__impl p eq = replace p (sym eq)

%rewrite HEqual rewrite__impl

headReducePlug :
  (ctx : EvalContext a b) -> (r : Redex a) ->
  Equal (headReduce (plug ctx (redex r))) (plug ctx (contract r))
headReducePlug ctx r
  = replace (\ x => Equal (recompose x) (plug ctx (contract r))) (sym (decomposePlug ctx (redex r)))
  (replace (\ x => Equal (recompose x) (plug ctx (contract r))) (sym (loadRedex ctx r))
  Refl)

data Void : Type where {}

Not : Type -> Type
Not x = x -> Void

absurd : (0 x : Void) -> a
absurd x impossible

headReduceNeutral :
  (ctx : EvalContext s b) -> (r : Redex s) ->
  (f : Closed (Arr a b)) -> Not (IsValue f) ->
  Equal (ClApp f t) (plug ctx (redex r)) ->
  Equal (plug ctx (contract r)) (ClApp (headReduce f) t)
headReduceNeutral ctx@_ r f nv with (snocView ctx)
  headReduceNeutral ctx@_ (RVar _ _) f nv | Lin = \ x => case x of Refl impossible
  headReduceNeutral ctx@_ (RApp _ _ _) f nv | Lin = \ x => case x of Refl impossible
  headReduceNeutral ctx@_ (Beta _ _ _) f nv | Lin = \ x => case x of Refl => absurd (nv Lam)
  headReduceNeutral ctx@_  r f nv | Snoc ctx' arg
    = replace (\ x => Equal (ClApp f t) (plug (snoc ctx' arg) (redex r)) -> Equal x (ClApp (headReduce f) t))
      (sym (plugSnoc ctx' arg (contract r)))
    (replace (\ x => Equal (ClApp f t) x -> Equal (ClApp (plug ctx' (contract r)) arg) (ClApp (headReduce f) t))
      (sym (plugSnoc ctx' arg (redex r)))
    (\ x => case x of
       Refl => replace (\ x => Equal (ClApp (plug ctx' (contract r)) arg) (ClApp x arg))
                   (sym (headReducePlug ctx' r)) Refl))

trans : (0 p : Equal x y) -> (0 q : Equal y z) -> Equal x z
trans Refl Refl = Refl

headReduceClApp : {a, b : Ty} ->
                  (f : Closed (Arr a b)) -> Not (IsValue f) -> (t : Closed a) ->
                  Equal (headReduce (ClApp f t)) (ClApp (headReduce f) t)
headReduceClApp f nv t =

  let
    go : forall a, b.
       (f : Closed (Arr a b)) -> Not (IsValue f) -> (t : Closed a) ->
       (ft : Closed b) -> Equal (ClApp f t) ft -> Decomposition ft ->
       Equal (headReduce ft) (ClApp (headReduce f) t)
    go f nv t (Closure (Lam sc) env) eq (Val sc env) = case eq of Refl impossible
    go f nv t (plug ctx (redex r)) eq (Red r ctx)
      = trans (headReducePlug ctx r) (headReduceNeutral ctx r f nv eq)

  in  go f nv t (ClApp f t) Refl (decompose (ClApp f t))

------------------------------------------------------------------------
-- Section 4: Iterated head reduction

namespace NaÃ¯ve

  covering
  evaluate : {a : Ty} -> Closed a -> Value a
  evaluate c =

    let
      iterate : {a : Ty} -> {0 c : Closed a} -> Decomposition c -> Value a
      iterate (Val b env)
         -- there's a typo in the paper for this case
        = Val (Closure (Lam b) env) Lam
      iterate (Red r ctx) = iterate (decompose (plug ctx (contract r)))

    in iterate (decompose c)

-- Using the Bove-Capretta technique


data Trace : Decomposition c -> Type where
  Done : (sc, env : _) -> Trace (Val sc env)
  Step : (ctx : EvalContext a b) -> (r : Redex a) ->
         Trace (decompose (plug ctx (contract r))) -> Trace (Red r ctx)

iterate : {a : Ty} -> {0 c : Closed a} -> {d : Decomposition {a} c} ->
          (0 x : Trace d) -> Value a
iterate (Done sc env) = Val (Closure (Lam sc) env) Lam
iterate (Step r ctx tr) = iterate tr

namespace Pair

  public export
  data Pair : (a, b : Type) -> Type where
    MkPair : {0 a, b : _} -> a -> b -> Pair a b

  public export
  fst : {0 a, b : _} -> Pair a b -> a
  fst (MkPair x y) = x

  public export
  snd : {0 a, b : _} -> Pair a b -> b
  snd (MkPair x y) = y

namespace DPair

  public export
  data DPair : (a : Type) -> (b : a -> Type) -> Type where
    MkDPair : {0 a, b : _} -> (x : a) -> b x -> DPair a b

  public export
  fst : {0 a, b : _} -> DPair a b -> a
  fst (MkDPair x y) = x

  public export
  snd : {0 a, b : _} -> (p : DPair a b) -> b (fst p)
  snd (MkDPair x y) = y

Reducible : (a : Ty) -> Closed a -> Type
Reducible Base t = Trace (decompose t)
Reducible (Arr a b) t
  = Pair (Trace (decompose t))
         ((y : Closed a) -> Reducible a y -> Reducible b (ClApp t y))

namespace ReducibleEnv

  public export
  data ReducibleEnv : Env g -> Type where
    Nil : ReducibleEnv Nil
    Cons : {0 a, t, env : _} -> Reducible a t -> ReducibleEnv env -> ReducibleEnv (Cons t env)

  public export
  lookup : {0 a, g, env : _} -> ReducibleEnv env -> (v : Elem a g) -> Reducible a (lookup env v)
  lookup (Cons red _) Here = red
  lookup (Cons _ reds) (There v) = lookup reds v

step : {a : Ty} -> {c : Closed a} -> Trace (decompose (headReduce c)) -> Trace (decompose c)
step {c} tr with (decompose c)
  step {c = _} tr | Val sc env = tr
  step {c = _} tr | Red r ctx = Step ctx r tr

expand : {a : Ty} -> {c : Closed a} -> Reducible a (headReduce c) -> Reducible a c
expand {a = Base} tr = step tr
expand {a = Arr a b} {c = Closure (Lam x) env} (MkPair tr hored) = MkPair (step tr) hored
expand {a = Arr a b} {c = Closure (App x t) env} (MkPair tr hored)
  = MkPair (step tr) (\ arg, red => expand (hored arg red))
expand {a = Arr a b} {c = Closure (Var x) env} (MkPair tr hored)
  = MkPair (step tr) (\ arg, red => expand (hored arg red))
expand {a = Arr a b} {c = ClApp f t} (MkPair tr hored)
  = MkPair (step tr)
  (\ arg, red =>
    let 0 eq = headReduceClApp (ClApp f t) (\ x => case x of Lam impossible) arg in
    let red = replace (Reducible b) (sym eq) (hored arg red) in
    expand {c = ClApp (ClApp f t) arg} red)

the : (0 a : Type) -> a -> a
the a x = x

closure : {a : Ty} -> (t : Term g a) -> {env : Env g} ->
          ReducibleEnv env -> Reducible a (Closure t env)
closure (Lam b) reds = MkPair (Done b _) (\ arg, red => expand (closure b (Cons red reds)))
closure {a = b} (App {a} f t) reds
  = expand ((snd (closure {a = Arr a b} f reds)) (Closure t env) (closure {a} t reds))
closure (Var v) reds = expand (lookup reds v)


theorem : {a : Ty} -> (c : Closed a) -> Reducible a c

theoremEnv : (env : Env g) -> ReducibleEnv env

theorem (Closure t env) = closure t (theoremEnv env)
theorem (ClApp f t) = snd (theorem f) t (theorem t)

theoremEnv Nil = Nil
theoremEnv (Cons t env) = Cons (theorem t) (theoremEnv env)


termination : {a : Ty} -> (c : Closed a) -> Trace (decompose c)
termination {a = Base} c = theorem c
termination {a = Arr a b} c = fst (theorem c)


evaluate : {a : Ty} -> Closed a -> Value a
evaluate c = iterate (termination c)

------------------------------------------------------------------------
-- Section 5: Refocusing


refocus : {a : Ty} -> (ctx : EvalContext a b) -> (c : Closed a) -> Decomposition (plug ctx c)
refocus ctx (ClApp f t) = refocus (Cons t ctx) f
refocus ctx (Closure (App f t) env) = Red (RApp f t env) ctx
refocus ctx (Closure (Var v) env) = Red (RVar v env) ctx
refocus Nil (Closure (Lam b) env) = Val b env
refocus (Cons t ctx) (Closure (Lam b) env) = Red (Beta b env t) ctx

refocusCorrect : {a : Ty} -> (ctx : EvalContext a b) -> (c : Closed a) ->
                 Equal (refocus ctx c) (decompose (plug ctx c))
refocusCorrect ctx (ClApp f t) = refocusCorrect (Cons t ctx) f
refocusCorrect ctx (Closure (App f t) env) = sym (decomposePlug ctx (Closure (App f t) env))
refocusCorrect ctx (Closure (Var v) env) = sym (decomposePlug ctx (Closure (Var v) env))
refocusCorrect Nil (Closure (Lam b) env) = Refl
refocusCorrect (Cons t ctx) (Closure (Lam b) env) = sym (decomposePlug (Cons t ctx) (Closure (Lam b) env))

namespace Refocus

  public export
  data Trace : {c : Closed a} -> Decomposition c -> Type where
    Done : (0 sc, env : _) -> Trace (Val sc env)
    Step : {0 a, b : Ty} -> (0 ctx : EvalContext a b) -> (0 r : Redex a) ->
           Refocus.Trace (refocus ctx (contract r)) -> Trace (Red r ctx)

  export
  trace : {0 a : Ty} -> {0 c : Closed a} -> {0 d, e : Decomposition c} ->
          Main.Trace d -> (0 x : Equal e d) -> Refocus.Trace e
  trace (Done sc env) Refl = Done sc env
  trace (Step ctx r tr) Refl
    = Step ctx r (trace tr (refocusCorrect ctx (contract r)))

  export
  termination : {a : Ty} -> (c : Closed a) -> Refocus.Trace (decompose c)
  termination c = trace (termination c) Refl

  export
  iterate : {0 a, c : _} -> {d : Decomposition {a} c} -> (0 x : Refocus.Trace d) -> Value a
  iterate (Done sc env) = Val (Closure (Lam sc) env) Lam
  iterate (Step ctx r tr) = iterate tr

  export
  evaluate : {a : Ty} -> Closed a -> Value a
  evaluate c = iterate (Refocus.termination c)

------------------------------------------------------------------------
-- Section 6: The Krivine machine

data Unit : Type where
  MkUnit : Unit


IsValidEnv : Env g -> Type

IsValidClosed : Closed a -> Type

IsValidClosed (Closure t env) = IsValidEnv env
IsValidClosed _ = Void

IsValidEnv Nil = Unit
IsValidEnv (Cons t env) = Pair (IsValidClosed t) (IsValidEnv env)

namespace Subset

  public export
  record Subset (a : Type) (p : a -> Type) where
    constructor Element
    fst : a
    snd : p fst

ValidEnv : Context -> Type
ValidEnv g = Subset (Env g) IsValidEnv

ValidClosed : Ty -> Type
ValidClosed a = Subset (Closed a) IsValidClosed

namespace ValidClosed

  public export
  Closure : {0 g, a : _} -> Term g a -> ValidEnv g -> ValidClosed a
  Closure t (Element env pr) = Element (Closure t env) pr

  public export
  fstClosure : {0 g, a : _} -> (t : Term g a) -> (env : ValidEnv g) ->
               Equal (fst (Closure t env)) (Closure t (fst env))
  fstClosure t (Element env p) = Refl

  public export
  0 getContext : {0 a : _} -> ValidClosed a -> Context
  getContext (Element (Closure {g} t env) _) = g

  public export
  getEnv : {0 a : _} -> (c : ValidClosed a) -> ValidEnv (getContext c)
  getEnv (Element (Closure {g} _ env) pr) = Element env pr

  public export
  getTerm : {0 a : _} -> (c : ValidClosed a) -> Term (getContext c) a
  getTerm (Element (Closure t _) _) = t

  public export
  etaValidClosed : {0 a : _} -> (c : ValidClosed a) -> Equal c (Closure (getTerm c) (getEnv c))
  etaValidClosed (Element (Closure t env) _) = Refl

namespace ValidEnv

  export
  lookup : {0 g, a : _} -> (env : ValidEnv g) -> Elem a g -> ValidClosed a
  lookup (Element (Cons t env) p) Here = Element t (fst p)
  lookup (Element (Cons t env) p) (There v) = lookup (Element env (snd p)) v

  public export
  fstLookup : {0 g, a : _} -> (env : ValidEnv g) -> (v : Elem a g) ->
              Equal (fst (lookup env v)) (lookup (fst env) v)
  fstLookup (Element (Cons t env) p) Here = Refl
  fstLookup (Element (Cons t env) p) (There v) = fstLookup (Element env (snd p)) v

  public export
  Nil : ValidEnv Nil
  Nil = Element Nil MkUnit

  public export
  Cons : {0 g : _} -> {a : Ty} -> ValidClosed a -> ValidEnv g -> ValidEnv (Cons a g)
  Cons (Element t p) (Element env q) = Element (Cons t env) (MkPair p q)


IsValidEvalContext : EvalContext a b -> Type
IsValidEvalContext Nil = Unit
IsValidEvalContext (Cons t ctx) =  Pair (IsValidClosed t) (IsValidEvalContext ctx)

ValidEvalContext : (a, b : Ty) -> Type
ValidEvalContext a b = Subset (EvalContext a b) IsValidEvalContext

record Biinjective {0 a, b, c : Type} (0 f : a -> b -> c) where
  constructor MkBiinjective
  runBiinjective : (x, y : a) -> (v, w : b) -> Equal (f x v) (f y w) -> Pair (Equal x y) (Equal v w)

biinjective : Biinjective {a} {b} {c} f =>
              {x, y : a} -> {v, w : b} -> Equal (f x v) (f y w) -> Pair (Equal x y) (Equal v w)
biinjective @{bi} = runBiinjective bi _ _ _ _

namespace ValidEvalContext


  public export
  Nil : {0 a : Ty} -> ValidEvalContext a a
  Nil = Element Nil MkUnit

  public export
  Cons : {0 a, b, c : Ty} -> ValidClosed a -> ValidEvalContext b c -> ValidEvalContext (Arr a b) c
  Cons (Element t p) (Element ctx q) = Element (Cons t ctx) (MkPair p q)

  fstCons : {0 a, b, c : Ty} ->
            (t : ValidClosed a) -> (ctx : ValidEvalContext b c) ->
            Equal (fst (Cons t ctx)) (Cons (fst t) (fst ctx))
  fstCons (Element t p) (Element ctx q) = Refl

  public export
  CONS : Biinjective ValidEvalContext.Cons
  CONS =
    let biinjective : {0 a, b, c : Ty} ->
         (x, y : ValidClosed a) -> (v, w : ValidEvalContext b c) ->
         Equal (ValidEvalContext.Cons x v) (ValidEvalContext.Cons y w) ->
         Pair (Equal x y) (Equal v w)
        biinjective
         (Element t p) (Element t p)
         (Element ts ps) (Element ts ps)
         Refl = MkPair Refl Refl

    in MkBiinjective biinjective


namespace ValidEvalContextView

  public export
  data View : ValidEvalContext a b -> Type where
    Nil : View Nil
    Cons : {0 a, b, c : Ty} -> (t : ValidClosed a) -> (ctx : ValidEvalContext b c) ->
           View (Cons t ctx)

  export
  irrelevantUnit : (t : Unit) -> Equal t MkUnit
  irrelevantUnit MkUnit = Refl

  export
  etaPair : {0 a, b : Type} -> (p : Pair a b) -> Equal p (MkPair (fst p) (snd p))
  etaPair (MkPair x y) = Refl

  export
  view : {0 a, b : Ty} -> (ctx : ValidEvalContext a b) -> View ctx
  view (Element Nil p)
    = replace (\ prf => View {a} {b = a} (Element Nil prf)) (sym (irrelevantUnit p)) Nil
  view (Element (Cons t ctx) p)
    = replace (\ prf => View (Element (Cons t ctx) prf)) (sym (etaPair p))
    (Cons (Element t (fst p)) (Element ctx (snd p)))

namespace Machine

  data Trace : Term g a -> ValidEnv g -> ValidEvalContext a b -> Type where

    Var : {0 g, a, b : _} -> {0 ctx : ValidEvalContext a b} ->
          {env : ValidEnv g} -> {v : Elem a g} ->
          {0 g' : Context} -> {0 t' : Term g' a} -> {0 env' : Env g'} -> {0 prf' : IsValidEnv env'} ->
          Equal (lookup env v) (Element (Closure t' env') prf') ->
          Trace t' (Element env' prf') ctx ->
          Trace (Var v) env ctx

    App : {0 g, a, b, c, env : _} -> {0 ctx : ValidEvalContext b c} ->
          {f : Term g (Arr a b)} -> {t : Term g a} ->
          Trace f env (Cons (Closure t env) ctx) ->
          Trace (App f t) env ctx

    Beta : {0 g, a, b, c, env : _} ->
           {sc : Term (Cons a g) b} ->
           {arg : ValidClosed a} ->
           {ctx : ValidEvalContext b c} ->
           Trace sc (Cons arg env) ctx ->
           Trace (Lam sc) env (Cons arg ctx)

    Done : {0 sc, env : _} ->
           Trace (Lam sc) env Nil

  data View : Trace t env ctx -> Type where
    VVar : {0 g, a, b : _} -> {0 ctx : ValidEvalContext a b} ->
           {0 env : ValidEnv g} -> {0 v : Elem a g} ->
           {0 g' : _} -> {t', env' : _} -> {0 prf' : _} ->
           (0 eq : Equal (lookup env v) (Element (Closure {g = g'} t' env') prf')) ->
           (0 tr : Trace t' (Element env' prf') ctx) ->
           View {t = Var v} {env} {ctx} (Var eq tr)
    VApp : {0 g, a, b, c, env : _} ->
           {0 f : Term g (Arr a b)} -> {0 t : Term g a} ->
           {0 ctx : ValidEvalContext b c} ->
           (0 tr : Trace f env (Cons (Closure t env) ctx)) ->
           View (App {f} {t} {env} {ctx} tr)
    VBeta : {0 g, a, b, c, env : _} ->
            {0 sc : Term (Cons a g) b} ->
            {arg : ValidClosed a} ->
            {ctx : ValidEvalContext b c} ->
            (0 tr : Trace sc (Cons arg env) ctx) ->
            View (Beta {sc} {arg} {env} {ctx} tr)
    VDone : {0 g, a, b, env : _} ->
            (sc : Term (Cons a g) b) ->
            View (Done {sc} {env})

  -- nasty inversion principle
  data IsVar :
    {0 g, a, b : _} -> {0 env : ValidEnv g} ->
    {0 ctx : ValidEvalContext a b} -> {0 v : Elem a g} ->
    (tr : Trace (Var v) env ctx) -> Type where
    MkIsVar :
     {0 g, a, b : _} -> {0 env : ValidEnv g} ->
     {0 ctx : ValidEvalContext a b} -> {0 v : Elem a g} ->
     {0 tr : Trace (Var v) env ctx} ->
     {0 g' : Context} ->
     {0 t' : Term g' a} ->
     {0 env' : Env g'} ->
     {0 prf' : IsValidEnv env'} ->
     (0 eq : Equal (lookup env v) (Element (Closure t' env') prf')) ->
     (tr' : Trace t' (Element env' prf') ctx) ->
     (inversion : Equal tr (Var eq tr')) ->
     IsVar tr

  vvar : {0 g, a, b : _} -> {0 env : ValidEnv g} ->
         {0 ctx : ValidEvalContext a b} -> {0 v : Elem a g} ->
         (tr : Trace (Var v) env ctx) ->
         IsVar tr
  vvar (Var eq tr) = MkIsVar eq tr Refl

  vapp : {0 env, ctx, f, t : _} ->
         (tr : Trace (App f t) env ctx) ->
         DPair (Trace f env (Cons (Closure t env) ctx)) (\ tr' => Equal tr (App tr'))
  vapp (App tr) = MkDPair tr Refl

  vlam0 : {0 g, a, b, env : _} -> {0 ctx : ValidEvalContext (Arr a b) (Arr a b)} ->
          {sc : Term (Cons a g) b} ->
          (eq : Equal ctx Nil) ->
          (tr : Trace (Lam sc) env ctx) ->
          HEqual tr (Machine.Done {sc} {env})
  vlam0 eq Done = Refl

  vlamS : {0 g, a, b, c, ctx : _} -> {0 env : ValidEnv g} -> {0 arg : ValidClosed a} ->
          {0 sc : Term (Cons a g) b} -> {0 ctx' : ValidEvalContext b c} ->
          (eq : Equal ctx (ValidEvalContext.Cons arg ctx')) ->
          (tr : Trace (Lam sc) env ctx) ->
          DPair (Trace sc (Cons arg env) ctx')
                (\ tr' => HEqual tr (Machine.Beta {sc} {arg} {env} tr'))
  vlamS eq {env} {arg} {sc} (Beta {env} {arg = arg''} {ctx = ctx''} tr) =
    let

      go : {0 arg, arg'' : ValidClosed a} -> {0 ctx'', ctx' : ValidEvalContext b c} ->
           (tr : Trace {g = Cons a g} {a = b} {b = c} sc (Cons arg'' env) ctx'') ->
           (0 eq1 : Equal arg'' arg) -> (0 eq2 : Equal ctx'' ctx') ->
           DPair (Trace {g = Cons a g} {a = b} {b = c} sc (Cons arg env) ctx')
                 (\ tr' => HEqual (Beta {a} {b} {c} {env} {sc} {arg = arg''} {ctx = ctx''} tr)
                                  (Machine.Beta {a} {b} {c} {sc} {arg} {env} {ctx = ctx'} tr'))
      go tr Refl Refl = MkDPair tr Refl

      0 eqs : Pair (Equal arg'' arg) (Equal ctx'' ctx')
      eqs = biinjective @{CONS} {x = arg''} {y = arg} {v = ctx''} {w = ctx'} eq

    in go tr (fst eqs) (snd eqs)

  withProof : {0 a : Type} -> (x : a) -> DPair a (Equal x)
  withProof x = MkDPair x Refl

  cong : {0 a, b : Type} -> (f : a -> b) ->
         {0 x, y : a} -> (0 eq : Equal x y) -> Equal (f x) (f y)
  cong f Refl = Refl

  view : {0 g, a, b : _} ->
         (t : Term g a) -> (env : ValidEnv g) -> (ctx : ValidEvalContext a b) ->
         (0 tr : Trace t env ctx) -> View tr
  view (Var v) env ctx tr with 0 (vvar tr)
    view (Var v) env ctx (Var eq tr') | MkIsVar eq tr' Refl =
      let go : {0 g' : Context} -> {0 t' : Term g' a} ->
               {0 env' : Env g'} -> {0 prf' : IsValidEnv env'} ->
               {0 g'' : Context} -> {t'' : Term g'' a} ->
               {env'' : Env g''} -> {0 prf'' : IsValidEnv env''} ->
               (0 eq : Equal (lookup env v) (Element (Closure t' env') prf')) ->
               (0 cast : Equal {a = ValidClosed a}
                          (Element (Closure t' env') prf')
                          (Element (Closure t'' env'') prf'')) ->
               (0 tr' : Trace t' (Element env' prf') ctx) ->
               Machine.View (Var {v} {env} eq tr')
          go eq Refl tr' = VVar eq tr'
      in case withProof (lookup env v) of
           MkDPair (Element (Closure t'' env'') prf'') leq =>
             go eq (trans (sym eq) leq) tr'
  view (App f t) env ctx tr = rewrite snd (vapp tr) in VApp _
  view (Lam sc) env ctx@_ tr with (view ctx)
    view (Lam sc) env Nil tr | Nil = rewrite vlam0 Refl tr in VDone sc
    view (Lam sc) env ctx@_ tr | (Cons arg ctx')
      = rewrite snd (vlamS {env} {arg} {sc} {ctx'} Refl tr) in VBeta _

  refocus : {0 g, b : _} -> {a : Ty} ->
    {t : Term g a} -> {env : ValidEnv g} ->
    {ctx : ValidEvalContext a b} ->
    (0 tr : Trace t env ctx) -> Value b
  refocus tr@_ with (view _ _ _ tr)
    refocus tr@_ | VVar eq tr' = refocus tr'
    refocus tr@_ | VApp tr' = refocus tr'
    refocus tr@_ | VBeta tr' = refocus tr'
    refocus tr@_ | VDone sc = Val (Closure (Lam sc) (fst env)) Lam

  -- This is way TOO nasty because we don't have eta for records :((

{-
  correctness :
    {0 g, b : _} ->
    {a : Ty} ->
    (ctx : ValidEvalContext a b) ->
    (t : Term g a) ->
    (env : ValidEnv g) ->
    (trold : Refocus.Trace (refocus (fst ctx) (Closure t (fst env)))) ->
    (trnew : Machine.Trace t env ctx) ->
    Equal (refocus trnew) (Refocus.iterate trold)
  correctness ctx (Var v) env (Step (fst ctx) (RVar v (fst env)) trold) (Var trnew)
    with (lookup env v) proof eq
    correctness ctx (Var v) env (Step (fst ctx) (RVar v (fst env)) trold) (Var trnew)
    | Element (Closure t' env') penv'
    = correctness ctx t' (Element env' penv')
        (rewrite sym (cong fst eq) in rewrite fstLookup env v in trold)
        trnew
  correctness ctx (App f t) env (Step (fst ctx) (RApp f t (fst env)) trold) (App trnew)
    = correctness (Cons (Closure t env) ctx) f env
        (rewrite fstCons (Closure t env) ctx in rewrite fstClosure t env in trold)
        trnew
  correctness (Cons (Element arg parg) (Element ctx pctx)) (Lam sc) (Element env penv)
    (Step ctx (Beta sc env arg) trold)
    (Beta {arg = Element arg parg} {ctx = Element ctx pctx} trnew)
    = correctness (Element ctx pctx) sc (Cons (Element arg parg) (Element env penv))
        trold
        trnew
  correctness (Element Nil _) (Lam sc) env (Done _ _) tr
    -- DISGUSTING
    = case tr of
        Beta {arg = Element _ _} {ctx = Element _ _} _ impossible
        Done => Refl

{-
  -- Another disgusgint proof because of a mix of:
  --   * lack of eta and the coverage
  --   * invalid "Dot pattern not valid here (Not LHS)" on the LHS
  --   * coverage checker being confused

  trace : {a : Ty} -> (ctx : ValidEvalContext a b) ->
          (t : Term g a) -> (env : ValidEnv g) ->
          Refocus.Trace (refocus (fst ctx) (Closure t (fst env))) ->
          Trace t env ctx
  trace ctx (Var v) env (Step (fst ctx) (RVar v (fst env)) tr)
    with (fstLookup env v) | (lookup env v) proof eq
    _ | lemma | Element (Closure t' env') p
      with (lookup (fst env) v)
      trace ctx (Var v) env (Step (fst ctx) (RVar v (fst env)) tr)
        | Refl | Element (Closure t' env') penv' | .(Closure t' env')
        = Var (rewrite eq in trace ctx t' (Element env' penv') tr)
  trace (Element ctx pctx) (App f t) (Element env penv) (Step .(ctx) (RApp f t env) tr)
    = App (trace (Closure t (Element env penv) :: Element ctx pctx) f (Element env penv) tr)
  trace (Element (arg :: ctx) pctx) (Lam sc) (Element env penv) tr
    = case tr of
        Done _ _ impossible
        (Step ctx (Beta sc env arg) tr) =>
          rewrite etaPair pctx in
          Beta {arg = Element arg (fst pctx), ctx = Element ctx (snd pctx)}
          (trace (Element ctx (snd pctx)) sc (Element (arg :: env) (fst pctx, penv)) tr)
  trace (Element Nil pctx) (Lam sc) env tr
    = case tr of
        Beta {arg = Element _ _, ctx = Element _ _} _ impossible
        Done sc .(fst env) => rewrite irrelevantUnit pctx in Done


  termination : {a : Ty} -> (t : Term Nil a) -> Trace t Nil Nil
  termination t
    = trace Nil t Nil
    $ rewrite refocusCorrect Nil (Closure t Nil) in
      Refocus.termination (Closure t Nil)


  evaluate : {a : Ty} -> Term Nil a -> Value a
  evaluate t = Machine.refocus (termination t)
-}